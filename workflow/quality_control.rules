import pandas as pd
from snakemake.utils import validate

configfile: "config_ongoing_run.yaml"
validate(config, "config.qc.schema.yaml")

key = config["KEY"]
input_path = config["READSPATH"]
samples = pd.read_csv(config["METAFILE"], sep = '\t', header = 0)['sample']
# validate samples?

end = config["END"]
final_path = config["RESULTPATH"] + "/" + config["PROJECT"]

if input_path[0] != "/": 
    import os
    input_path = os.getcwd()+"/"+input_path

    
if config["SRA"] == "yes" or config["SRA"] == True:
    catlogs = final_path + "/fastqc/SRA.log"
else: 
    catlogs = []

rule end:
    input:
        report = final_path + "/fastqc/report_quality_control.html", 
        cat_logs = catlogs      
        
if config["SRA"] == "yes" or config["SRA"] == True: 
        
        rule configFastqDump:
            output:
                ncbi_config = ".ncbi/user-settings.mkfg"
            singularity:
                "rasflow_edc.simg"
            shell:
                """
                cp /ncbi/user-settings.mkfg .ncbi
                """
        
if end == "pair":

    if config["SRA"] == "yes" or config["SRA"] == True: 

        rule fastqDump:
            input: 
                ncbi_config = ".ncbi/user-settings.mkfg"
            output:
                fastq_R1 = input_path + "/{sample}_R1.fastq.gz",
                fastq_R2 = input_path + "/{sample}_R2.fastq.gz"
            singularity:
                "rasflow_edc.simg"
            resources:
                mem_mb=10000, 
                cpus=4
            params:
                file_1 = input_path + "/{sample}_1.fastq.gz",
                file_2 = input_path + "/{sample}_2.fastq.gz"
            log: 
                input_path + "/{sample}.log"
            shell:
                """
                parallel-fastq-dump --sra-id {wildcards.sample} --threads 4 --outdir {input_path} --split-files \
                --gzip --tmpdir {input_path} &> {log}
                mv {params.file_1} {output.fastq_R1}
                mv {params.file_2} {output.fastq_R2}
                """
                
        rule catLogs:
            input: 
                logs = expand(input_path + "/{sample}.log", sample=samples)
            output: 
                catlogs = final_path + "/fastqc/SRA.log"
            shell:
                "sed -s -e $'$a\\\n-----' {input_path}/*.log > {output.catlogs}" 

            
    rule qualityControl:
        input:
            fw = input_path + "/{sample}_R1.fastq.gz",
            rv = input_path + "/{sample}_R2.fastq.gz"
        output:
            fastqc_fw = final_path + "/fastqc/{sample}_R1_fastqc.html",
            fastqc_rv = final_path + "/fastqc/{sample}_R2_fastqc.html"
        singularity:
            "rasflow_edc.simg"
        resources:
           mem_mb=6000, 
           cpus=2
        params:
            outputpath = final_path + "/fastqc"
        shell:
            "fastqc -t 2 -o {params.outputpath} {input.fw} {input.rv}"
            
    rule MultiQC:
        input:
            fastqc_fw = expand(final_path + "/fastqc/{sample}_R1_fastqc.html", sample = samples),
            fastqc_rv = expand(final_path + "/fastqc/{sample}_R2_fastqc.html", sample = samples)
        output:
            report = final_path + "/fastqc/report_quality_control.html",
            folder = directory(final_path + "/fastqc/report_quality_control_data")
        singularity:
            "rasflow_edc.simg"
        resources:
           mem_mb=1000
        params:
            path = final_path + "/fastqc"
        shell:
            "rm -fr {output.report} {output.folder} && multiqc {params.path} --filename {output.report}"
            
else:  
    
    if config["SRA"] == "yes" or config["SRA"] == True: 
    
        rule fastqDump:
            input: 
                ncbi_config = ".ncbi/user-settings.mkfg"
            output:
                fastq = input_path + "/{sample}.fastq.gz"         
            singularity:
                "rasflow_edc.simg"
            resources:
                mem_mb=10000, 
                cpus=4
            log: 
                input_path + "/{sample}.log"
            shell:
                """
                parallel-fastq-dump --sra-id {wildcards.sample} --threads 4 --outdir {input_path} \
                --gzip --tmpdir {input_path} &> {log}
                """
                
        rule catLogs:
            input: 
                logs = expand(input_path + "/{sample}.log", sample=samples)
            output: 
                catlogs = final_path + "/fastqc/SRA.log"
            shell:
                "sed -s -e $'$a\\\n-----' {input_path}/*.log > {output.catlogs}" 
        
            
    rule qualityControl:
        input:
            fastq = input_path + "/{sample}.fastq.gz"
        output:
            fastqc = final_path + "/fastqc/{sample}_fastqc.html"
        singularity:
            "rasflow_edc.simg"
        resources:
           mem_mb=6000
        params:
            outputpath = final_path + "/fastqc"
        shell:
            "fastqc -o {params.outputpath} {input.fastq}"
            
    rule MultiQC:
        input:
            fastqc = expand(final_path + "/fastqc/{sample}_fastqc.html", sample = samples)
        output:
            report = final_path + "/fastqc/report_quality_control.html",
            folder = directory(final_path + "/fastqc/report_quality_control_data")
        singularity:
            "rasflow_edc.simg"
        resources:
           mem_mb=1000
        params:
            path = final_path + "/fastqc"
        shell:
            "rm -fr {output.report} {output.folder} && multiqc {params.path} --filename {output.report}"
    
